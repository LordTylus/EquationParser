plugins {
    id 'java-library'
    id 'jacoco'
    id 'maven-publish'
}

group 'io.github.lordtylus.equation'
version '1.1.0'

repositories {
    mavenCentral()
}

sourceSets {
    create("demo") {
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }
}

dependencies {

    compileOnly 'org.projectlombok:lombok:1.18.24'
    annotationProcessor 'org.projectlombok:lombok:1.18.24'

    testImplementation 'org.assertj:assertj-core:3.27.3'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.7.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
}

java {

    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17

    withSourcesJar()
    withJavadocJar()
}

jacocoTestReport {
    reports {
        csv.required.set(true)
        xml.required.set(true)
    }
}

test {
    useJUnitPlatform()
    finalizedBy(tasks.jacocoTestReport)
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/LordTylus/EquationParser")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
                password = project.findProperty("gpr.key") ?: System.getenv("TOKEN")
            }
        }
    }
    publications {
        mavenJava(MavenPublication) {

            artifactId 'equation-parser'

            from(components.java)

            pom {
                name.set("Equation Parser")
                description.set("A generic and extendable framework to parse expressions and mathematical equations such as 4*(2+x)^2.")
                url.set("https://github.com/LordTylus/EquationParser")

                licenses {
                    license {
                        name.set("The Apache License, Version 2.0")
                        url.set("https://www.apache.org/licenses/LICENSE-2.0.txt")
                    }
                }

                developers {
                    developer {
                        id.set("LordTylus")
                        name.set("Martin RÃ¶kker")
                        email.set("martin.roekker@email.com")
                    }
                }

                scm {
                    connection.set("scm:git:git://github.com/LordTylus/EquationParser.git")
                    developerConnection.set("scm:git:ssh://github.com:LordTylus/EquationParser.git")
                    url.set("https://github.com/LordTylus/EquationParser.git")
                }
            }
        }
    }
}